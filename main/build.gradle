import org.apache.tools.ant.filters.ReplaceTokens

archivesBaseName = 'SimpleChatFormat'

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT'
    compileOnly "com.github.MilkBowl:VaultAPI:${versionVault}"

    implementation project(':impl:1.16')
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: rootProject.version]
    }
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    archiveVersion.set(rootProject.version)
}

task copyToServer(type: Copy) {
    def dir = System.getenv('PLUGINS_DIR')
    if (dir == null) return true

    from("${buildDir}/libs") {
        include "${archivesBaseName}-${rootProject.version}.jar"
    }
    into dir
}

assemble {
    finalizedBy copyToServer
}