plugins {
    id 'java'
}

import org.apache.tools.ant.filters.ReplaceTokens

group = 'de.axelrindle.simplechatformat'
version = '1.0'
archivesBaseName = 'SimpleChatFormat'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    versionSpigot = '1.16.4'
    versionVault = '1.7'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:${versionSpigot}-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:${versionVault}"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: rootProject.version]
    }
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

task copyToServer(type: Copy) {
    def dir = System.getenv('PLUGINS_DIR')
    if (dir == null) return true

    from("${buildDir}/libs") {
        include "${archivesBaseName}-${rootProject.version}.jar"
    }
    into dir
}

assemble {
    finalizedBy copyToServer
}